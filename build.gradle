// Build plugins
plugins {
	id 'java'
	id 'idea'
	id 'jacoco'
	id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.github.node-gradle.node' version '2.2.3'
	id 'org.sonarqube' version '2.8'
	id 'net.researchgate.release' version '2.8.1'
	id 'com.gorylenko.gradle-git-properties' version '2.2.2'
	id 'org.owasp.dependencycheck' version '5.3.2'
}

// Java
sourceCompatibility = 1.8

// Spring Boot
springBoot {
	buildInfo()
}

// Release
release.buildTasks=['bootBuildImage']

// Node/NPM
node {
	version = '12.16.1'
	npmVersion = '6.13.4'
	download = true
	nodeModulesDir = file('ui')
}
task npmBuild(type: NpmTask, dependsOn: npmInstall) {
	npmCommand = ['run', 'build-prod']
}
processResources.dependsOn(npmBuild)
test.dependsOn(npm_test)
sourceSets {
	main.resources.srcDirs 'ui/dist'
}
clean {
	delete 'ui/dist'
}

// Jacoco test coverage
jacoco.toolVersion '0.8.5'
jacocoTestReport.reports.html.destination file("${buildDir}/reports/coverage/test")
plugins.withType(JacocoPlugin) {
	tasks['test'].finalizedBy jacocoTestReport
}

// Dependency vulnerability scanning
dependencyCheck {
	suppressionFile = 'dependency-check-suppression.xml'
	failBuildOnCVSS = 4
	// CVSS scoring: https://nvd.nist.gov/vuln-metrics/cvss
	// None     0.0
	// Low      0.1-3.9
	// Medium   4.0-6.9
	// High     7.0-8.9
	// Critical 9.0-10.0
}

// Project dependencies
repositories {
	mavenCentral()
}
dependencies {
	implementation 		'org.springframework.boot:spring-boot-starter-web'
	implementation 		'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 		'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 		'org.springframework.boot:spring-boot-starter-data-ldap'
	implementation 		'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 		'org.springframework.boot:spring-boot-starter-actuator'
	implementation 		'org.springframework.boot:spring-boot-starter-security'
	implementation		'org.springframework.boot:spring-boot-starter-validation'
	implementation 		'org.springframework.session:spring-session-data-redis'
	// The following dependency has been deprecated, however the functionality is expected to be reimplemented in spring-security in the future.
	// See https://github.com/spring-projects/spring-security/issues/6320
	implementation 		'org.springframework.security.oauth:spring-security-oauth2:2.4.1.RELEASE'
	implementation 		'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.2.6.RELEASE'
	//
	implementation 		'com.unboundid:unboundid-ldapsdk'
	implementation		'com.google.guava:guava:28.2-jre' // Overriding version due to CVEs in 21.0/20.0 (used in embedded-redis + swagger)
	implementation 		'it.ozimov:embedded-redis:0.7.2'
	implementation 		'io.springfox:springfox-swagger2:2.9.2'
	implementation 		'io.springfox:springfox-swagger-ui:2.9.2'
	compileOnly 		'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	runtimeOnly		 	'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 		'com.h2database:h2'
	runtimeOnly 		'com.oracle.ojdbc:ojdbc8:19.3.0.0'
	testImplementation 	'org.springframework.boot:spring-boot-starter-test'
	testImplementation 	'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 	'org.springframework.security:spring-security-test'
}
