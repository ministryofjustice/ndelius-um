// Build plugins
plugins {
	id 'java'
	id 'idea'
	id 'jacoco'
	id 'org.springframework.boot'               version '3.0.0'
	id 'io.spring.dependency-management'        version '1.0.11.RELEASE'
	id 'com.github.node-gradle.node'            version '3.3.0'
	id 'io.gatling.gradle'                      version '3.7.6.3'
	id 'org.sonarqube'                          version '3.4.0.2513'
	id 'com.gorylenko.gradle-git-properties'    version '2.4.1'
	id 'org.owasp.dependencycheck'              version '7.1.1'
}

// Java
java {
	toolchain { languageVersion = JavaLanguageVersion.of(11) }
}

// Spring Boot
springBoot { buildInfo() }
gitProperties { failOnNoGitDirectory = false }
jar { enabled = false }

// Node/NPM
node {
	version = '16.13.1'
	download = true
	nodeModulesDir = file('ui')
}
task npmBuild(type: NpmTask, dependsOn: npmInstall) {
	npmCommand = ['run', 'build-prod']
}
processResources.dependsOn(npmBuild)
test.dependsOn(npm_test)
sourceSets { main.resources.srcDirs 'ui/dist' }
clean { delete 'ui/dist' }

// Jacoco test coverage
jacoco.toolVersion '0.8.7'
jacocoTestReport.reports.html.destination file("${buildDir}/reports/coverage/test")
plugins.withType(JacocoPlugin) {
	tasks['test'].finalizedBy jacocoTestReport
}

// Dependency vulnerability scanning
dependencyCheck {
	suppressionFile = 'dependency-check-suppression.xml'
	analyzers.assemblyEnabled = false
	skipConfigurations = ['zinc'] // zinc is the Scala incremental compiler used for Gatling tests, and can be ignored
	failBuildOnCVSS = 4 // Medium severity or higher. See https://nvd.nist.gov/vuln-metrics/cvss
}

// Project dependencies
ext['h2.version'] = '2.1.210' // overriding managed dependency due to various CVEs in h2 1.x
repositories { mavenCentral() }
dependencies {
	implementation      'org.springframework.boot:spring-boot-starter-web'
	implementation      'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation      'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation      'org.springframework.boot:spring-boot-starter-data-ldap'
	implementation      'org.springframework.boot:spring-boot-starter-data-redis'
	implementation      'org.springframework.boot:spring-boot-starter-actuator'
	implementation      'org.springframework.boot:spring-boot-starter-security'
	implementation      'org.springframework.boot:spring-boot-starter-validation'
	implementation      'org.springframework.session:spring-session-data-redis'
	implementation      'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'
	implementation      'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.6.8'
	implementation      'org.bouncycastle:bcpkix-jdk15on:1.70' // Overriding dependency of spring-security-oauth2-autoconfigure due to CVE-2020-15522
	implementation      'com.unboundid:unboundid-ldapsdk'
	implementation      'org.signal:embedded-redis:0.8.3'
	implementation      'com.google.guava:guava:31.1-jre' // Overriding dependency of embedded-redis:0.8.1 due to CVE-2020-8908
	implementation      'org.springdoc:springdoc-openapi-ui:1.6.9'
	implementation      'com.opencsv:opencsv:5.6'
	implementation      'io.github.resilience4j:resilience4j-bulkhead:1.7.1'
	implementation      'io.github.resilience4j:resilience4j-spring-boot2:1.7.1'
	compileOnly         'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	developmentOnly     'org.springframework.boot:spring-boot-devtools'
	runtimeOnly         'com.h2database:h2'
	runtimeOnly         'com.oracle.database.jdbc:ojdbc11'
	testImplementation  'org.junit.vintage:junit-vintage-engine'
	testImplementation  'org.springframework.boot:spring-boot-starter-test'
	testImplementation  'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation  'org.springframework.security:spring-security-test'
}
