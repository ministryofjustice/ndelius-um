// Build dependencies
buildscript {
	ext {
		springBootVersion = '2.1.5.RELEASE'
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.moowork.gradle:gradle-node-plugin:1.3.1")
		classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8")
		classpath("net.researchgate:gradle-release:2.8.1")
	}
}

// Build plugins
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'net.researchgate.release'

// Java
sourceCompatibility = 1.8
sourceSets {
    main.resources.srcDirs 'src/main/resources', 'ui/dist'
}

// Spring Boot
springBoot {
	buildInfo()
}

// Node/NPM
node {
	version = '12.13.1'
	npmVersion = '6.12.1'
	download = true
}
npmInstall.workingDir = file("${project.projectDir}/ui")
npm_test.workingDir = file("${project.projectDir}/ui")
npm_test.dependsOn(npmInstall)
npm_run.workingDir = file("${project.projectDir}/ui")
npm_run.args = ['build-prod']
npm_run.dependsOn(npm_test)
processResources.dependsOn(npm_run)

// Jacoco test coverage
jacoco.toolVersion '0.8.5'
jacocoTestReport.reports.html.destination file("${buildDir}/reports/coverage/test")
plugins.withType(JacocoPlugin) {
	tasks['test'].finalizedBy jacocoTestReport
}

// Project dependencies
repositories {
	mavenCentral()
}
dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-ldap')
	compile('org.springframework.boot:spring-boot-starter-data-redis')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.session:spring-session-data-redis')
	compile('org.springframework.security.oauth:spring-security-oauth2:2.3.8.RELEASE')
	compile('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.2.2.RELEASE')
	compile('com.unboundid:unboundid-ldapsdk')
	compile('it.ozimov:embedded-redis:0.7.2')
	compile('io.springfox:springfox-swagger2:2.9.2')
	compile('io.springfox:springfox-swagger-ui:2.9.2')
	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('com.h2database:h2')
	runtime files('lib/ojdbc8-12.2.0.1.jar')
	compileOnly('org.projectlombok:lombok')
	compileOnly('org.springframework.boot:spring-boot-configuration-processor')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
	testCompile('org.springframework.security:spring-security-test')
}
