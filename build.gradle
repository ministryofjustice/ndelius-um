// Build plugins
plugins {
	id 'java'
	id 'idea'
	id 'jacoco'
	id 'org.springframework.boot'               version '2.4.9'
	id 'io.spring.dependency-management'        version '1.0.11.RELEASE'
	id 'com.github.node-gradle.node'            version '3.1.0'
	id 'org.sonarqube'                          version '3.3'
	id 'com.gorylenko.gradle-git-properties'    version '2.3.1'
	id 'org.owasp.dependencycheck'              version '6.2.2'
}

// Java
sourceCompatibility = 11

// Spring Boot
springBoot { buildInfo() }
gitProperties { failOnNoGitDirectory = false }

// Node/NPM
node {
	version = '12.18.3'
	download = true
	nodeModulesDir = file('ui')
}
task npmBuild(type: NpmTask, dependsOn: npmInstall) {
	npmCommand = ['run', 'build-prod']
}
processResources.dependsOn(npmBuild)
test.dependsOn(npm_test)
sourceSets {
	main.resources.srcDirs 'ui/dist'
}
clean {
	delete 'ui/dist'
}

// Jacoco test coverage
jacoco.toolVersion '0.8.5'
jacocoTestReport.reports.html.destination file("${buildDir}/reports/coverage/test")
plugins.withType(JacocoPlugin) {
	tasks['test'].finalizedBy jacocoTestReport
}

// Dependency vulnerability scanning
dependencyCheck {
	suppressionFile = 'dependency-check-suppression.xml'
	failBuildOnCVSS = 4
	// CVSS scoring: https://nvd.nist.gov/vuln-metrics/cvss
	// None     0.0
	// Low      0.1-3.9
	// Medium   4.0-6.9
	// High     7.0-8.9
	// Critical 9.0-10.0
}

// Project dependencies
repositories {
	mavenCentral()
}
dependencies {
	implementation      'org.springframework.boot:spring-boot-starter-web'
	implementation      'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation      'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation      'org.springframework.boot:spring-boot-starter-data-ldap'
	implementation      'org.springframework.boot:spring-boot-starter-data-redis'
	implementation      'org.springframework.boot:spring-boot-starter-actuator'
	implementation      'org.springframework.boot:spring-boot-starter-security'
	implementation      'org.springframework.boot:spring-boot-starter-validation'
	implementation      'org.springframework.session:spring-session-data-redis'
	implementation      'org.springframework.security.oauth:spring-security-oauth2:2.5.1.RELEASE'
	implementation      'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.4.8'
	implementation      'com.unboundid:unboundid-ldapsdk'
	implementation      'it.ozimov:embedded-redis:0.7.2'
	implementation      'com.google.guava:guava:30.1.1-jre' // Overriding dependency of embedded-redis due to CVE-2018-10237, CVE-2020-8908
	implementation      'commons-io:commons-io:2.11.0'      // Overriding dependency of embedded-redis due to CVE-2021-29425
	implementation      'io.springfox:springfox-swagger2:2.9.2'
	implementation      'io.springfox:springfox-swagger-ui:2.9.2'
	implementation      'com.opencsv:opencsv:5.5.1'
	implementation      'io.github.resilience4j:resilience4j-bulkhead:1.7.1'
	implementation      'io.github.resilience4j:resilience4j-spring-boot2:1.7.1'
	compileOnly         'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	developmentOnly     'org.springframework.boot:spring-boot-devtools'
	runtimeOnly         'com.h2database:h2'
	runtimeOnly         'com.oracle.ojdbc:ojdbc8'
	testImplementation  'org.junit.vintage:junit-vintage-engine'
	testImplementation  'org.springframework.boot:spring-boot-starter-test'
	testImplementation  'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation  'org.springframework.security:spring-security-test'
}
